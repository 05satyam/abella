%% Start generic section

Def nabla x, nominal x.
Def nabla x, fresh x E.

Theorem fresh_tail : forall X A L,
  fresh X (A :: L) -> fresh X L.
intros. case H1. search.

Theorem member_fresh : forall X L E,
  member E L -> fresh X L -> fresh X E.
induction on 1. intros. case H1.
  case H2. search.
  apply fresh_tail to H2. apply IH to H3 H4. search.

%% End generic section

Def ctx nil.
Def ctx (bound X U :: L) :- fresh X L, ctx L.

Def ty L top.
Def ty L X :- member (bound X U) L.
Def ty L (arrow T1 T2) :- ty L T1, ty L T2.
Def ty L (all T1 T2) :- ty L T1, (nabla x, ty (bound x T1 :: L) (T2 x)).

Theorem sub_refl : forall L T,
  ty L T -> {L |- sub T T}.
induction on 1. intros. case H1.
  search.
  search.
  apply IH to H2. apply IH to H3. search.
  apply IH to H2. case H3. apply IH to H5. search.

Theorem no_ctx_sub : forall S T L,
  ctx L -> member (sub S T) L -> false.
induction on 1. intros. case H1.
  case H2.
  case H2.
    apply IH to H4 H5. search.

Theorem ctx_nominal : forall L X U,
  ctx L -> member (bound X U) L -> nominal X.
induction on 1. intros. case H1.
  case H2.
  case H2.
    case H3. search.
    apply IH to H4 H5. search.

%% This proof would be much nicer if we could unify over metaterms,
%% instead we use a _ and prove the obligation later.
Theorem permute_weaken : forall M N S T,
  ctx M -> ctx N -> (forall B, member B M -> member B N) -> {M |- sub S T} ->
    {N |- sub S T}.
induction on 4. intros. case H4.
  apply no_ctx_sub to H1 H5. case H6.
  search.
  case H5. apply H3 to H6. search.
  case H5. apply H3 to H7. apply IH to H1 H2 _ H6.
    intros. apply H3 to H9. search.
    search.
  apply IH to H1 H2 _ H5. intros. apply H3 to H7. search.
    apply IH to H1 H2 _ H6. intros. apply H3 to H8. search.
      search.
  apply IH to H1 H2 _ H5. intros. apply H3 to H7. search.
    assert nabla x, ctx (bound x T1 :: N). search. case H8.
    apply IH to _ H9 _ H6.
      skip. % Introducing B here should raise B over nominals
      search.
        
    
    


