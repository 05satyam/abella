%% Proof that each lambda term is normal or non-normal
%%
%% The ctxpair predicate encodes the relationship between the context
%% of the judgements (term T) and (normal T).

Def ctxpair nil nil.
Def ctxpair (term X :: L1) (var X :: L2) :- ctxpair L1 L2.

Theorem ctxpair_lemma : forall X L1 L2,
  ctxpair L1 L2 -> member (term X) L1 -> member (var X) L2.
induction on 1. intros. case H1.
  case H2.
  case H2.
    search.
    apply IH to H3 H4.
      search.

Theorem ctxpair_lemma2 : forall X L1 L2,
  ctxpair L1 L2 -> member (normal X) L2 -> false.
induction on 1. intros. case H1.
  case H2.
  case H2.
    apply IH to H3 H4.
      search.

      
Theorem generalized : forall T L1 L2,
  ctxpair L1 L2 -> {L1 |- term T} -> {L2 |- normal T} \/ {non_normal T}.
induction on 2. intros. case H2.
  apply ctxpair_lemma to H1 H3. search.
  apply IH to H1 H3. apply IH to H1 H4. case H5.
    case H6.
      case H7.
        apply ctxpair_lemma2 to H1 H9. case H10.
        search.
        search.
        search.
      search.
    search.
  apply IH to _ H3.
  case H4.
  search.
  search.

  
Theorem specialized : forall T,
  {term T} -> {normal T} \/ {non_normal T}.
intros.
apply generalized to _ H1.
case H2.
  search.
  search.
