%% Cut elimination for sequent calculus with "and" and "implies"
%%
%% An object level sequent A_1, ..., A_n |- C is encoded as
%% {hyp A_1, ..., hyp A_n |- conc C}. We use the ctx meta-level
%% predicate to describe the structure of contexts for the conc
%% predicate. In this case, the important point is that the context
%% contains only hypotheses (hyp A) and not conclusions (conc A).

Def ctx nil.
Def ctx (hyp A :: L) :- ctx L.

Theorem ctx_lemma :
  forall A L, ctx L -> member (conc A) L -> false.
induction on 1. intros. case H1.
  case H2.
  case H2.
    apply IH to H3 H4. search.

    
Theorem and_left_inv :
  forall L C1 C2, ctx L -> {L |- conc (and C1 C2)} -> {L |- conc C1}.
induction on 2. intros. case H2.
  apply ctx_lemma to H1 H3. case H4.
  search.
  search.
  apply IH to _ H4. search.
  apply IH to _ H5. search.

    
Theorem and_right_inv :
  forall L C1 C2, ctx L -> {L |- conc (and C1 C2)} -> {L |- conc C2}.
induction on 2. intros. case H2.
  apply ctx_lemma to H1 H3. case H4.
  search.
  search.
  apply IH to _ H4. search.
  apply IH to _ H5. search.

  
Theorem imp_inv :
  forall L C1 C2, ctx L -> {L |- conc (imp C1 C2)} -> {L, hyp C1 |- conc C2}.
induction on 2. intros. case H2.
  apply ctx_lemma to H1 H3. case H4.
  search.
  apply IH to _ H4. search.
  search.
  apply IH to _ H5. search.


Theorem cut_elim :
  forall L K C,
    {prop K} -> ctx L -> {L |- conc K} -> {L, hyp K |- conc C} -> {L |- conc C}.
induction on 1. induction on 4. intros. case H4.
  apply ctx_lemma to _ H5. case H6.
  case H5. case H6.
    search.
    search.
  search.
  apply IH' to H1 _ H3 H5. apply IH' to H1 _ H3 H6.
    search.
  apply IH' to H1 _ H3 H6. case H5. case H8.
    apply and_left_inv to _ H3. apply and_right_inv to _ H3. case H1.
      apply IH to H11 _ H9 H7. apply IH to H12 _ H10 H13. search.
      search.
  apply IH' to H1 _ H3 H5. search.
  apply IH' to H1 _ H3 H6. apply IH' to H1 _ H3 H7.
    case H5. case H10.
      apply imp_inv to _ H3. case H1.
        apply IH to H12 _ H8 H11. apply IH to H13 _ H14 H9. search.
      search.
