%% Disjoint partitioning of lambda terms into normal and non-normal form

%% The ctxs predicate encodes the relationship between the context
%% of the judgements (term T) and (normal T).

Define nabla x, name x.

Define ctxs nil nil.
Define nabla x, ctxs (term x :: L) (var x :: K) := ctxs L K.


%% Lemmas about contexts

Theorem ctxs_member_term : forall X L K,
  ctxs L K -> member (term X) L -> member (var X) K.
induction on 1. intros. case H1.
  case H2.
  case H2.
    search.
    apply IH to H3 H4.
      search.

Theorem ctxs_var : forall E L K,
  ctxs L K -> member E K -> exists X, E = var X /\ name X.
induction on 1. intros. case H1.
  case H2.
  case H2.
    search.
    apply IH to H3 H4. search.


%% Partitioning is total
      
Theorem total_ext : forall T L K,
  ctxs L K -> {L |- term T} -> {K |- normal T} \/ {non_normal T}.
induction on 2. intros. case H2.
  apply ctxs_member_term to H1 H3. search.
  apply IH to H1 H3. apply IH to H1 H4. case H5.
    case H6.
      case H7.
        apply ctxs_var to H1 H9.
        search.
        search.
        search.
      search.
    search.
  apply IH to _ H3. case H4.
    search.
    search.

Theorem total : forall T,
  {term T} -> {normal T} \/ {non_normal T}.
intros. apply total_ext to _ H1. search.


%% Partitioning is disjoint

Theorem normal_head_normal : forall L K T,
  ctxs L K -> {K |- normal_head T} -> {K |- normal T}.
intros. case H2.
  apply ctxs_var to H1 H3.
  search.
  search.

Theorem disjoint_ext : forall L K T,
  ctxs L K -> {K |- normal T} -> {non_normal T} -> false.
induction on 3. intros. case H2.
  apply ctxs_var to H1 H4.
  case H3. apply IH to _ H4 H5.
  case H3.
    case H4.
      apply ctxs_var to H1 H6.
      case H6. apply ctxs_var to H1 H7. case H8.
    apply normal_head_normal to H1 H4.
      apply IH to H1 H7 H6.
    apply IH to H1 H5 H6.
  case H4. apply ctxs_var to H1 H5. case H6. case H3.

Theorem disjoint : forall T,
  {normal T} -> {non_normal T} -> false.
intros. apply disjoint_ext to _ H1 H2.
