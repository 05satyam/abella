%% Prove the equivalence of two predicates where one uses a hidden auxillary

Specification "hidden".

Query {rev1 (cons n1 (cons n2 (cons n3 nill))) (R n1 n2 n3)}.
Query {rev2 (cons n1 (cons n2 (cons n3 nill))) (R n1 n2 n3)}.

Theorem sound :
  forall A B, {rev2 A B} -> {rev1 A B}.
intros. case H1. inst H2 with n1 = aux1.
assert {pi x1\aux1 nill x1 x1}.
cut H3 with H4.
assert {pi x1\pi x2\ pi x3\ pi x4\aux1 x2 (cons x1 x3) x4 =>
                                  aux1 (cons x1 x2) x3 x4}.
cut H5 with H6.
search.

Theorem lemma :
  forall AA BB CC, {aux1 AA BB CC} ->
    {pi aux2\
      (pi C\ aux2 nill C C) =>
      (pi X\ pi A\ pi B\ pi C\ aux2 A (cons X B) C => aux2 (cons X A) B C) =>
        aux2 AA BB CC}.
induction on 1. intros. case H1.
  search.
  apply IH to H2. search.

Theorem complete :
  forall A B, {rev1 A B} -> {rev2 A B}.
intros. case H1. apply lemma to H2. search.
